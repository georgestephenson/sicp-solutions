## Exercise 3.26

In exercise 2.66 I gave an implementation of a lookup where a list of key-value pairs are represented as a binary tree where the key is numeric and can be ordered numerically:

``` Scheme
(define (lookup given-key set)
  (cond ((null? set) false)
        ((= given-key (key (entry set))) (entry set))
        ((< given-key (key (entry set)))
         (lookup given-key (left-branch set)))
        ((> given-key (key (entry set)))
         (lookup given-key (right-branch set)))))
```

To have a table stored as a binary tree, the same idea would be applied for `lookup`. The trickier implementation will be `insert!`, where we need to add to the key-value pair to the set, or replace the existing value if the key already exists.

``` Scheme
(define (make-table)
  (define local-table '())
  (define key-record car)
  (define value-record cdr)
 
  (define (make-tree entry left right) (list entry left right))
  (define entry car)
  (define left-branch cadr)
  (define right-branch caddr)
 
  (define (adjoin-set x set)
    (cond ((null? set) (make-tree x '() '()))
          ((= (key-record x) (key-record (entry set))) 
           (make-tree x
                      (left-branch set)
                      (right-branch set)))
          ((< (key-record x) (key-record (entry set)))
           (make-tree (entry set)
                      (adjoin-set x (left-branch set))
                      (right-branch set)))
          (else
           (make-tree (entry set)
                      (left-branch set)
                      (adjoin-set x (right-branch set))))))
 
  (define (lookup key records)
    (if (null? records)
        false
        (let* ((record (entry records))
               (key-entry (key-record record)))
          (cond ((= key key-entry) (value-record record))
                ((< key key-entry) (lookup key (left-branch records)))
                (else (lookup key (right-branch records)))))))
 
  (define (insert! key value)
    (set! local-table
          (adjoin-set (cons key value)
                      local-table)))
 
  (define (dispatch m)
    (cond ((eq? m 'lookup-proc) (lambda (key) (lookup key local-table)))
          ((eq? m 'insert-proc!) insert!)
          (else (error "Unknown operation -- TABLE" m))))
  dispatch)

(define t1 (make-table))
((t1 'insert-proc!) 0 5)
((t1 'lookup-proc) 0)
;Value: 5
((t1 'insert-proc!) 1 3)
((t1 'lookup-proc) 1)
;Value: 3
((t1 'insert-proc!) 0 99)
((t1 'lookup-proc) 0)
;Value: 99
```