## Exercise 2.73

### Part A

```
(define (deriv exp var)
   (cond ((number? exp) 0)
         ((variable? exp) (if (same-variable? exp var) 1 0))
         (else ((get 'deriv (operator exp)) (operands exp)
                                            var))))

(define (operator exp) (car exp))

(define (operands exp) (cdr exp))
```

In the above code snippet, `(operator exp)` is used to retrieve the algebraic operator symbol, which is used as the "type tag".

For the `get` procedure as defined in section 2.4.3, `'deriv` is passed as the operation, and `(operator exp)` is the type, which then returns the corresponding procedure for the derivation of the expression `exp`, passing `(operands exp)` and `var` to this procedure.

We use this approach for most of our `deriv` rules, but for the special cases of `number?` and `same-variable?`, these procedures don't depend on an `operator` and take `operands` and `var` as arguments. These cases are checking are checking if `exp` is a primitive number or a variable (symbol) matching the independent variable `var`.

### Part B

For full source code including `get` and `put` implementation, taken from the official SICP site, see `resources/2-73.scm`.

```
(define (install-deriv-package)
  ;; internal procedures
  (define (deriv-sum operands var)
    (make-sum (deriv (addend operands) var)
              (deriv (augend operands) var)))
  (define (addend s) (car s))
  (define (augend s) (cadr s))
  (define (deriv-product operands var)
    (make-sum
      (make-product (multiplier operands)
                    (deriv (multiplicand operands) var))
      (make-product (deriv (multiplier operands) var)
                    (multiplicand operands))))
  (define (multiplier p) (car p))
  (define (multiplicand p) (cadr p))
  (define (make-sum a1 a2) (list '+ a1 a2))
  (define (make-product m1 m2) (list '* m1 m2))

  ;; interface to the rest of the system
  (put 'deriv '+ deriv-sum)
  (put 'deriv '* deriv-product)
  'done)

(install-deriv-package)
(deriv '(* (* x y) (+ x 3)) 'x)
;Value: (+ (* (* x y) (+ 1 0)) (* (+ (* x 0) (* 1 y)) (+ x 3)))
```

### Part C

Adding `deriv-exponentiation`.

```
(define (install-deriv-package)
  ;; internal procedures
  (define (deriv-sum operands var)
    (make-sum (deriv (addend operands) var)
              (deriv (augend operands) var)))
  (define (addend s) (car s))
  (define (augend s) (cadr s))
  (define (deriv-product operands var)
    (make-sum
      (make-product (multiplier operands)
                    (deriv (multiplicand operands) var))
      (make-product (deriv (multiplier operands) var)
                    (multiplicand operands))))
  (define (multiplier p) (car p))
  (define (multiplicand p) (cadr p))
  (define (deriv-exponentiation operands var)
    (make-product (exponent operands)
                  (make-product (make-exponentiation (base operands) 
                                                     (make-sum (exponent operands) -1))
                                (deriv (base operands) var))))
  (define (base p) (car p))
  (define (exponent p) (cadr p))
  (define (make-sum a1 a2) (list '+ a1 a2))
  (define (make-product m1 m2) (list '* m1 m2))
  (define (make-exponentiation b e)
    (cond ((=number? e 0) 1)
          ((=number? e 1) b)
          ((and (number? b) (number? e)) (expt b e))
          (else (list '** b e))))
  (define (=number? exp num)
    (and (number? exp) (= exp num)))

  ;; interface to the rest of the system
  (put 'deriv '+ deriv-sum)
  (put 'deriv '* deriv-product)
  (put 'deriv '** deriv-exponentiation)
  'done)

(install-deriv-package)
(deriv '(+ (** x 5) (+ (* x y) (+ x 7))) 'x)
;Value: (+ (* 5 (* (** x (+ 5 -1)) 1)) (+ (+ (* x 0) (* 1 y)) (+ 1 0)))
```

### Part D

Swapping the "operation" and "type" within the `get` command will also produce a table of procedures, just pivoted. To use this, we just need to swap "operation" and "type" witin calls to `put` too.

```
(define (install-deriv-package)
  ;; internal procedures
  (define (deriv-sum operands var)
    (make-sum (deriv (addend operands) var)
              (deriv (augend operands) var)))
  (define (addend s) (car s))
  (define (augend s) (cadr s))
  (define (deriv-product operands var)
    (make-sum
      (make-product (multiplier operands)
                    (deriv (multiplicand operands) var))
      (make-product (deriv (multiplier operands) var)
                    (multiplicand operands))))
  (define (multiplier p) (car p))
  (define (multiplicand p) (cadr p))
  (define (deriv-exponentiation operands var)
    (make-product (exponent operands)
                  (make-product (make-exponentiation (base operands) 
                                                     (make-sum (exponent operands) -1))
                                (deriv (base operands) var))))
  (define (base p) (car p))
  (define (exponent p) (cadr p))
  (define (make-sum a1 a2) (list '+ a1 a2))
  (define (make-product m1 m2) (list '* m1 m2))
  (define (make-exponentiation b e)
    (cond ((=number? e 0) 1)
          ((=number? e 1) b)
          ((and (number? b) (number? e)) (expt b e))
          (else (list '** b e))))
  (define (=number? exp num)
    (and (number? exp) (= exp num)))

  ;; interface to the rest of the system
  (put '+ 'deriv deriv-sum)
  (put '* 'deriv deriv-product)
  (put '** 'deriv deriv-exponentiation)
  'done)

(define (deriv exp var)
   (cond ((number? exp) 0)
         ((variable? exp) (if (same-variable? exp var) 1 0))
         (else ((get (operator exp) 'deriv) (operands exp)
                                            var))))

(install-deriv-package)
(deriv '(+ (** x 5) (+ (* x y) (+ x 7))) 'x)
;Value: (+ (* 5 (* (** x (+ 5 -1)) 1)) (+ (+ (* x 0) (* 1 y)) (+ 1 0)))
```