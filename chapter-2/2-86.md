## Exercise 2.86

### Solution Description

We can implement complex numbers whose real and imaginary parts (or magnitude and angle) are different types of numbers, simply by reusing our existing number type packages, for example the packages for `'rational` and `'scheme-number`.

1. Wherever we previously used primitive numbers to represent the complex number parts, we use our own types. No change is required to the _operands_, as they are already abstract.
2. However, where we previously used primitive `operators` like `+` and `sin`, we must replace these with generic operations that handle all our number types. `add` has already been implemented in previous examples, we must implement generic `sin` which I'll call `gen-sin`.

### Reimplement `'complex'` package

```
(define (install-complex-package)
  ;; imported procedures from rectangular and polar packages
  (define (make-from-real-imag x y)
    ((get 'make-from-real-imag 'rectangular) x y))
  (define (make-from-mag-ang r a)
    ((get 'make-from-mag-ang 'polar) r a))
  ;; internal procedures
  (define (add-complex z1 z2)
    (make-from-real-imag (add (real-part z1) (real-part z2))
                         (add (imag-part z1) (imag-part z2))))
  (define (sub-complex z1 z2)
    (make-from-real-imag (sub (real-part z1) (real-part z2))
                         (sub (imag-part z1) (imag-part z2))))
  (define (mul-complex z1 z2)
    (make-from-mag-ang (mul (magnitude z1) (magnitude z2))
                       (add (angle z1) (angle z2))))
  (define (div-complex z1 z2)
    (make-from-mag-ang (div (magnitude z1) (magnitude z2))
                       (sub (angle z1) (angle z2))))

  ;; interface to rest of the system
  (define (tag z) (attach-tag 'complex z))
  (put 'add '(complex complex)
       (lambda (z1 z2) (tag (add-complex z1 z2))))
  (put 'sub '(complex complex)
       (lambda (z1 z2) (tag (sub-complex z1 z2))))
  (put 'mul '(complex complex)
       (lambda (z1 z2) (tag (mul-complex z1 z2))))
  (put 'div '(complex complex)
       (lambda (z1 z2) (tag (div-complex z1 z2))))
  (put 'make-from-real-imag 'complex
       (lambda (x y) (tag (make-from-real-imag x y))))
  (put 'make-from-mag-ang 'complex
       (lambda (r a) (tag (make-from-mag-ang r a))))
  (put 'higher? 'complex (lambda (x) #f))
  'done)
```

### Reimplement `'scheme-number` package

```
(define (install-scheme-number-package)
  (define (tag x)
    (attach-tag 'scheme-number x))
  (define (scheme-number->complex n)
    (make-complex-from-real-imag n 0))
  (put 'add '(scheme-number scheme-number)
       (lambda (x y) (tag (+ x y))))
  (put 'sub '(scheme-number scheme-number)
       (lambda (x y) (tag (- x y))))
  (put 'mul '(scheme-number scheme-number)
       (lambda (x y) (tag (* x y))))
  (put 'div '(scheme-number scheme-number)
       (lambda (x y) (tag (/ x y))))
  (put 'make 'scheme-number
       (lambda (x) (tag x)))
  (put 'gen-sin '(scheme-number) (lambda (x) (tag (sin x))))
  (put 'gen-cos '(scheme-number) (lambda (x) (tag (cos x))))
  (put 'gen-atan '(scheme-number) (lambda (x) (tag (atan x))))
  (put 'gen-square '(scheme-number) (lambda (x) (tag (square x))))
  (put 'gen-sqrt '(scheme-number) (lambda (x) (tag (sqrt x))))
  (put 'higher? 'scheme-number 
    (lambda (x) (or (eq? x 'complex) ((get 'higher? 'complex) x))))
  (put 'raise '(scheme-number) scheme-number->complex)
  'done)
```

### Reimplement `'rational` package

```
(define (install-rational-package)
  ;; internal procedures
  (define (numer x) (car x))
  (define (denom x) (cdr x))
  (define (make-rat n d)
    (let ((g (gcd n d)))
      (cons (/ n g) (/ d g))))
  (define (add-rat x y)
    (make-rat (+ (* (numer x) (denom y))
                 (* (numer y) (denom x)))
              (* (denom x) (denom y))))
  (define (sub-rat x y)
    (make-rat (- (* (numer x) (denom y))
                 (* (numer y) (denom x)))
              (* (denom x) (denom y))))
  (define (mul-rat x y)
    (make-rat (* (numer x) (numer y))
              (* (denom x) (denom y))))
  (define (div-rat x y)
    (make-rat (* (numer x) (denom y))
              (* (denom x) (numer y))))
  (define (rational->scheme-number rat)
    (make-scheme-number (/ (numer rat) (denom rat))))
  ;; interface to rest of the system
  (define (tag x) (attach-tag 'rational x))
  (put 'add '(rational rational)
       (lambda (x y) (tag (add-rat x y))))
  (put 'sub '(rational rational)
       (lambda (x y) (tag (sub-rat x y))))
  (put 'mul '(rational rational)
       (lambda (x y) (tag (mul-rat x y))))
  (put 'div '(rational rational)
       (lambda (x y) (tag (div-rat x y))))

  (put 'make 'rational
       (lambda (n d) (tag (make-rat n d))))
  (put 'gen-sin '(rational) (lambda (x) (make-scheme-number (sin (/ (numer x) (denom x))))))
  (put 'gen-cos '(rational) (lambda (x) (make-scheme-number (cos (/ (numer x) (denom x))))))
  (put 'gen-atan '(rational) (lambda (x) (make-scheme-number (atan (/ (numer x) (denom x))))))
  (put 'gen-square '(rational) (lambda (x) (tag (mul-rat x x))))
  (put 'gen-sqrt '(rational) (lambda (x) (make-scheme-number (sqrt (/ (numer x) (denom x))))))
  (put 'higher? 'rational 
    (lambda (x) (or (eq? x 'scheme-number) ((get 'higher? 'scheme-number) x))))
  (put 'raise '(rational) rational->scheme-number)
  'done)
```

### Generic Operators

```
(define (gen-sin x) (apply-generic 'gen-sin x))
(define (gen-cos x) (apply-generic 'gen-cos x))
(define (gen-atan x) (apply-generic 'gen-atan x))
(define (gen-square x) (apply-generic 'gen-square x))
(define (gen-sqrt x) (apply-generic 'gen-sqrt x))
```

*Full source code available in `resources/2-86.scm`*

Note: I have chosen to implement `gen-sin` on rational numbers by "raising" the number to a real number. Certainly there are values of $x$ where $\sin{x}$ is a rational number, and this can be worked out mathematically, and the same is true for other functions. We could avoid raising the number types for the cases it isn't necessary. But this would be a time-consuming subproblem. 

One solution would be to reuse `project` from Exercise 2.85 to try to project the result of primitive `sin` back to a rational number, for example. However, I would first need to implement `project` for rationals.

In order to raise rationals where required, I reuse `raise` from Exercise 2.83 and `higher?` from Exercise 2.84, to address any conflicts between types with coercion.

### Tests

```
(define complex1 (make-complex-from-real-imag (make-rational 4 5) (make-rational 1 3)))
complex1
;Value: (complex rectangular (rational 4 . 5) rational 1 . 3)

(define complex2 (make-complex-from-mag-ang (make-rational 3 2) (make-rational 7 9)))
complex2
;Value: (complex polar (rational 3 . 2) rational 7 . 9)

(add complex1 complex2)
;Value: (complex rectangular (scheme-number . 1.8687119368076284) scheme-number . 1.3858801475534361)
```