## Exercise 2.9

### Part A - Show Width of Sum is a Function of Widths being Added

The width of any interval $[a,A]$ can be expressed $(A-a)/2$

If $[b,B]$ is added to this interval, we know using Alyssa's reasoning that the resulting interval is

$$
[a+b,A+B]
$$

Applying our first rule, for the width of any interval, we get

$$
\frac{(A+B)-(a+b)}2 = \frac{(A-a)+(B-b)}2
$$

This is the same as adding the widths of both intervals.

If $[b,B]$ is subtracted from $[a,A]$, we know from Exercise 2.8 that the resulting interval is

$$
[a-B,A-b]
$$

Applying our first rule, for the width of any interval, we get

$$
\frac{(A-b)-(a-B)}2 = \frac{(A-a)+(B-b)}2
$$

This is also the same as adding the widths of both intervals.

### Part B - Examples Showing Width of Multiplication and Division Intervals are not Functions of Widths of Operands

We can test it by setting $a$ to different intervals and $b$ to different intervals but always having the same width for $a$ and same width for $b$.

We can see the width of the results are not equivalent in this case so are not functions only of the widths of $a$ and $b$.

```
(load "2-07.scm")

(define (mul-interval x y)
  (let ((p1 (* (lower-bound x) (lower-bound y)))
        (p2 (* (lower-bound x) (upper-bound y)))
        (p3 (* (upper-bound x) (lower-bound y)))
        (p4 (* (upper-bound x) (upper-bound y))))
    (make-interval (min p1 p2 p3 p4)
                   (max p1 p2 p3 p4))))

(define (div-interval x y)
  (mul-interval x 
                (make-interval (/ 1.0 (upper-bound y))
                               (/ 1.0 (lower-bound y)))))

(define (interval-width x)
  (/ (- (upper-bound x) (lower-bound x)) 2))

(define a (make-interval 65 67))
(interval-width a)
;Value: 1
(define b (make-interval 4 8))
(interval-width b)
;Value: 2

(interval-width (mul-interval a b))
;Value: 138
(interval-width (div-interval a b))
;Value: 4.3125

(define a (make-interval 11 13))
(interval-width a)
;Value: 1
(define b (make-interval 63 67))
(interval-width b)
;Value: 2

(interval-width (mul-interval a b))
;Value: 89
(interval-width (div-interval a b))
;Value: 2.1085050935797198e-2
```