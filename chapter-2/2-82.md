## Exercise 2.82

The difficulty level on this exercise seemed relatively high! I have put the full source code in `resources/2-82.scm`. It requires a lot of procedures from the textbook in order to test properly.

### Implementation of `apply-generic`

``` Scheme
(define (apply-generic op . args)
  (define (no-method-err op type-tags)
    (error "No method for these types" (list op type-tags)))
  (define (all-true? lst)
    (if (null? lst)
        #t
        (and (car lst) (all-true? (cdr lst)))))
  (define (get-coerce-map type-tags try-type)
    (map (lambda (t) (if (eq? t try-type)
                         identity
                         (get-coercion t try-type))) 
         type-tags))
  (define (find-coerce-map type-tags)
    (define (find-coerce-map-iter type-tags remaining)
      (if (null? remaining) 
          #f
          (let ((next (get-coerce-map type-tags (car remaining))))
            (if (all-true? next)
                next
                (find-coerce-map-iter type-tags (cdr remaining))))))
    (find-coerce-map-iter type-tags type-tags))
  (let ((type-tags (map type-tag args)))
    (let ((proc (get op type-tags)))
      (if proc
          (apply proc (map contents args))
          (let ((coerce-map (find-coerce-map type-tags)))
            (if coerce-map
                (apply apply-generic op (map (lambda (f x) (f x)) coerce-map args))
                (no-method-err op type-tags)))))))
```

### Test of `apply-generic`

I tested by implementing `add-complex` for three arguments. Obviously, it's possible to implement most of the generic arithmetic operators for $n$ arguments, but since the exercise didn't ask for it, I didn't go to that trouble.

``` Scheme
(apply-generic 'add 
  (make-complex-from-real-imag 1 2) 
  (make-scheme-number 7)
  (make-complex-from-real-imag 5 3))

;Value: (complex rectangular 13 . 5)
```

### Why the Strategy is not Sufficiently General 

In the original version of `apply-generic`, and my version for coercion of multiple arguments, there may by coercions that exist for the types in `type-tags` to some other external type. With the example given of isosceles triangle and right triangle (Figure 2.26), they don't coerce to each other, but they do both coerce to triangles. Furthermore, this can be true for one argument only, that one argument coerces to some other type, for which there exists a suitable mixed-type operation for all the other argument types as they stand.